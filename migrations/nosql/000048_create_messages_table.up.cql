CREATE TABLE IF NOT EXISTS messages (
  -- Primary Key: Channel + Message ID for locality
  -- Unique Id (ULID) (clustering key)
  id text, 
  -- Id of the channel this message was sent in (partition key)
  channel_id text, 
  -- Id of the user or webhook that sent this message
  author_id text, 
  
  -- Unique value generated by client (optional)
  nonce text, 
  -- The webhook that sent this message (optional)
  webhook frozen<message_webhook_type>, 
  -- Message content (optional)
  content text, 
  -- System message (renamed from system_type)
  "system" frozen<message_system_type>, 
  -- Array of attachments (optional)
  attachments frozen<list<frozen<file_type>>>, 
  -- Attached embeds (optional)
  embeds frozen<list<frozen<embed_type>>>, 
  -- Mentions
  -- Array of user ids mentioned (optional)
  mentions frozen<set<text>>, 
  -- Array of role ids mentioned (optional)
  role_mentions frozen<set<text>>, 
    -- Replies
  -- Array of message ids this replies to (optional)
  replies frozen<list<text>>, 
    -- Reactions: map<emoji_id, set<user_id>>
  reactions map<text,frozen<set<text>>>,
    -- Interactions
  interactions frozen<interactions_type>,
  -- Masquerade, Name/avatar overrides (optional)
  masquerade frozen<masquerade_type>, 
  -- Whether message is pinned (optional)
  pinned boolean, 
  -- Bitfield of message flags (optional)
  flags int, 
  
  -- Time at which this message was last edited (optional)
  edited_at timestamp, 
  -- When message was created (ULID timestamp)
  created_at timestamp, 
  -- Last update time
  updated_at timestamp, 
  
  PRIMARY KEY ((channel_id), id)
) WITH CLUSTERING ORDER BY (id DESC);
